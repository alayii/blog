---
title: SICP 笔记（3）
date: 2020-09-27T04:53:45.169Z
draft: true
---
## 符号数据

使用 `quote` 或者单引号表示符号本身，而不是符号表示的值。比如

```scheme
(define a 1)
(define b 2)

(list a b)
(1 2) ; 表示了值

(list 'a 'b)
(a b) ; 表示了 a, b 字符表示

(list 'a b)
(a 2)
```

进一步减小了数据和过程（procedure）的区别。

### Huffman 编码

Huffman 编码是一种变长编码形式，通常用来压缩信息。

完整的 Huffman tree：

![](/images/uploads/fig2.18a.std.svg)

根据字符出现的频率决定其权重，频率最低的要出现在离树根最远的地方，一旦确定了频率最低的两个值，再对其进行合并，则得到这两个叶子结点的父级，通过不断的递归，从而得到整颗树。

### 抽象数据的多重表示

需求会随时间变化而变化，不可能所有人都提前同意数据的表示方法。因此在数据抽象隔离使用和表示之外，还需要一层抽象隔离不同的设计选择并允许不同的选择共存。且大型系统通常由之前已经存在的单独设计的模块组合而成，需要使程序员可以往大型系统增加模块的约定，即不需要重新设计或实现已有模块。

复数系统示例：

![](/images/uploads/fig2.19a.std.svg)

竖线表示了数据的多重表示。

### Tagged data

数据可以使用 type-tag 来区分数据的具体表示。通用的 selector 则根据 type-tag 来确定如何处理这些数据。

给数据加上 type-tag 并没有改变数据的表示形式，而是通过 attach-tag 过程将其组合起来，并提供 `type-tag`，`contents` 方法来获取 tag 或内容。这样没有影响之前的底层实现。

### Data-Directed Programming and Additivity

按照 2.4.2 节的方式实现复数系统有两个缺点：

1. 通用的接口过程需要知道数据所有的不同表示，用于判断类型将数据应用到合适的选择器中。
2. 系统内的任意两个过程名称不能相同。

问题的根源在于实现通用接口的方法不是 additive（累加）的。

复数系统的操作名称，类型和实际的选择器可以描述为下图

![](/images/uploads/fig2.22.std.svg)


Data-Directed Programming 是一种设计程序的技术，用来直接与上图内的表工作。所以当有新的数据表示方式增加时，只需要更新表格即可。

分别用 put, get 方法更新和获取表内数据。

于是 rectangular 表示的复数可以实现如下：

```scheme
(define (install-rectangular-package)
  ;; internal procedures
  (define (real-part z) (car z))
  (define (imag-part z) (cdr z))
  (define (make-from-real-imag x y) 
    (cons x y))
  (define (magnitude z)
    (sqrt (+ (square (real-part z))
             (square (imag-part z)))))
  (define (angle z)
    (atan (imag-part z) (real-part z)))
  (define (make-from-mag-ang r a)
    (cons (* r (cos a)) (* r (sin a))))
  ;; interface to the rest of the system
  (define (tag x) 
    (attach-tag 'rectangular x))
  (put 'real-part '(rectangular) real-part)
  (put 'imag-part '(rectangular) imag-part)
  (put 'magnitude '(rectangular) magnitude)
  (put 'angle '(rectangular) angle)
  (put 'make-from-real-imag 'rectangular
       (lambda (x y) 
         (tag (make-from-real-imag x y))))
  (put 'make-from-mag-ang 'rectangular
       (lambda (r a) 
         (tag (make-from-mag-ang r a))))
  'done)
```

然后使用下面的过程来应用通用操作到参数上。它到表内查找操作名和参数类型，并应用返回的过程。

```scheme
(define (apply-generic op . args)
  (let ((type-tags (map type-tag args)))
    (let ((proc (get op type-tags)))
      (if proc
          (apply proc (map contents args))
          (error
            "No method for these types: 
             APPLY-GENERIC"
            (list op type-tags))))))
```

### Message passing

此种编程风格是将数据都表示为过程：

```scheme
(define (make-from-real-imag x y)
  (define (dispatch op)
    (cond ((eq? op 'real-part) x)
          ((eq? op 'imag-part) y)
          ((eq? op 'magnitude)
           (sqrt (+ (square x) (square y))))
          ((eq? op 'angle) (atan y x))
          (else
           (error "Unknown op: 
            MAKE-FROM-REAL-IMAG" op))))
  dispatch)
```

`make-from-real-imag` 没有返回任何数据，而是返回了 `dispatch` 这个过程，由它来做后续的操作。


`apply-generic` 定义为

```scheme
(define (apply-generic op arg) (arg op))
```

被称为 message passing 是因为，数据对象作为把请求的操作名作为 message。

